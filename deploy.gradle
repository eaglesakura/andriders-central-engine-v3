buildscript {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/eaglesakura/maven/" }
    }

    dependencies {
        classpath "com.eaglesakura:plugin-ci-support:1.2.200"
    }
}

apply plugin: 'com.eaglesakura.ci-support'
apply plugin: 'deploygate'

/**
 * フレーバー指定でapkを検索
 * @return 検索したAPKファイル or null
 */
private File getApk(String app, String flavor, String type) {
    File dir = file("ci-release/${app}/apk/");
    for (File file : dir.listFiles()) {
        println "check ${file.name}"
        if (flavor != null) {
            if ("${app}-${flavor}-${type}.apk".toString().equals(file.name)) {
                return file;
            }
        } else {
            if ("${app}-${type}.apk".toString().equals(file.name)) {
                return file;
            }
        }
    }
    return null;
}

/**
 * アルファ版: APKと更新情報をアップロードする
 */
task uploadGooglePlayAlphaVersion(type: com.eaglesakura.gradle.task.AndroidGooglePlayPublishTask) {
    applicationId = "org.andriders.ace"
    track = "alpha"
    apk = getApk("app", "googleplay", "release")
    serviceAccountEmail = GOOGLEPLAY_SERVICEACCOUNT_MAIL
    p12 = GOOGLEPLAY_DEPLOY_P12_FILE
}

/**
 * ベータ版: APKと更新情報をアップロードする
 */
task uploadGooglePlayBetaVersion(type: com.eaglesakura.gradle.task.AndroidGooglePlayPublishTask) {
    applicationId = "org.andriders.ace"
    track = "beta"
    apk = getApk("app", "googleplay", "release")
    apkListings = file("googleplay/beta")
    serviceAccountEmail = GOOGLEPLAY_SERVICEACCOUNT_MAIL
    p12 = GOOGLEPLAY_DEPLOY_P12_FILE
}

/**
 * 製品版: APKと更新情報をアップロードする
 */
task uploadGooglePlayReleaseVersion(type: com.eaglesakura.gradle.task.AndroidGooglePlayPublishTask) {
    applicationId = "org.andriders.ace"
    track = "production"
    apk = getApk("app", "googleplay", "release")
    apkListings = file("googleplay/production")
    serviceAccountEmail = GOOGLEPLAY_SERVICEACCOUNT_MAIL
    p12 = GOOGLEPLAY_DEPLOY_P12_FILE
}

/**
 * GooglePlay登録情報を送信する
 */
task uploadGooglePlayListings(type: com.eaglesakura.gradle.task.AndroidGooglePlayPublishTask) {
    applicationId = "org.andriders.ace";
    serviceAccountEmail = GOOGLEPLAY_SERVICEACCOUNT_MAIL
    listings = file("googleplay/production")
    p12 = GOOGLEPLAY_DEPLOY_P12_FILE
}
