buildscript {
    repositories {
        maven { url "http://eaglesakura.github.io/maven/" }
        jcenter()
    }

    dependencies {
        classpath("com.eaglesakura:plugin-ci-support:1.1.195") {
            exclude group: "com.google.apis"
            exclude group: "com.google.oauth-client"
        }
        classpath "com.google.apis:google-api-services-androidpublisher:v2-rev40-1.22.+"
        classpath "com.google.oauth-client:google-oauth-client-java6:1.22.+"
    }
}

apply plugin: 'com.eaglesakura.ci-support'
apply plugin: 'deploygate'

private String getDeployMessage() {
    try {
        return file("deploy-log.txt").text;
    } catch (Throwable e) {
        return "";
    }
}

/**
 * フレーバー指定でapkを検索
 * @return 検索したAPKファイル or null
 */
private File getApk(String app, String flavor, String type) {
    File dir = file("ci-release/${app}/apk/");
    for (File file : dir.listFiles()) {
        println "check ${file.name}"
        if (flavor != null) {
            if ("${app}-${flavor}-${type}.apk".toString().equals(file.name)) {
                return file;
            }
        } else {
            if ("${app}-${type}.apk".toString().equals(file.name)) {
                return file;
            }
        }
    }
    return null;
}


deploygate {

    userName = DEPLOYGATE_USER_NAME;
    token = DEPLOYGATE_API_KEY;

    apks {
        /**
         * googleplay debug
         */
        googleplayDebug {
            sourceFile = getApk("app", "googleplay", "debug");
            message = "for Googleplay Debug ${eglibrary.ci.buildDate}\n${getDeployMessage()}";
            releaseNote = "for Googleplay Debug | auto deploy | ${eglibrary.ci.buildDate}";
        }

        /**
         * googleplay release
         */
        googleplayRelease {
            sourceFile = getApk("app", "googleplay", "release");
            message = "for Googleplay Release ${eglibrary.ci.buildDate}\n${getDeployMessage()}";
            releaseNote = "for Googleplay Release | auto deploy | ${eglibrary.ci.buildDate}";
        }

    }
}

/**
 * アルファ版: APKと更新情報をアップロードする
 */
task uploadGooglePlayAlphaVersion(type: com.eaglesakura.gradle.task.AndroidGooglePlayPublishTask) {
    applicationId = "org.andriders.ace";
    track = "alpha";
    apk = getApk("app", "googleplay", "release")
    serviceAccountEmail = GOOGLEPLAY_SERVICEACCOUNT_MAIL;
    p12 = file("ace-private/release-deploy.p12")
}


/**
 * ベータ版: APKと更新情報をアップロードする
 */
task uploadGooglePlayBetaVersion(type: com.eaglesakura.gradle.task.AndroidGooglePlayPublishTask) {
    applicationId = "org.andriders.ace";
    track = "beta";
    apk = getApk("app", "googleplay", "release")
    serviceAccountEmail = GOOGLEPLAY_SERVICEACCOUNT_MAIL;
    p12 = file("ace-private/release-deploy.p12")
}

/**
 * 製品版: APKと更新情報をアップロードする
 */
task uploadGooglePlayReleaseVersion(type: com.eaglesakura.gradle.task.AndroidGooglePlayPublishTask) {
    applicationId = "org.andriders.ace";
    track = "production";
    apk = getApk("app", "googleplay", "release")
    serviceAccountEmail = GOOGLEPLAY_SERVICEACCOUNT_MAIL;
    p12 = file("ace-private/release-deploy.p12")
}

/**
 * GooglePlay登録情報を送信する
 */
task uploadGooglePlayListings(type: com.eaglesakura.gradle.task.AndroidGooglePlayPublishTask) {
    applicationId = "org.andriders.ace";
    serviceAccountEmail = GOOGLEPLAY_SERVICEACCOUNT_MAIL;
    listings = file("googleplay/listings")
    p12 = file("ace-private/release-deploy.p12")
}
